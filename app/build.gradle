import com.android.builder.model.ProductFlavor

apply plugin: 'com.android.application'
apply plugin: 'com.moowork.node'

android {
    compileSdkVersion 23
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.mozilla.webmaker"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 22
        versionName "1.6.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    flavorDimensions "abi"

    productFlavors {
        x86 {
            dimension "abi"
            ndk {
                abiFilters "x86", ""
            }
            versionCode = 3 as ProductFlavor
        }
        armv7 {
            dimension "abi"
            ndk {
                abiFilters "armeabi-v7a", ""
            }
            versionCode = 1 as ProductFlavor
        }
    }

    applicationVariants.all { variant ->
        // get the version code of each flavor
        def abiVersion = variant.productFlavors.get(0).versionCode

        // set the composite code
        variant.mergedFlavor.versionCode = abiVersion * 100000 + defaultConfig.versionCode
    }
}

task npmRunBuild (type: NpmTask, dependsOn: ['npmInstall']) {
    args = ['run', 'build']
}

task npmRunCopyCore (type: NpmTask, dependsOn: ['npmInstall']) {
    args = ['run', 'copy:core']
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleProductionRelease') {
        task.dependsOn npmRunBuild
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'org.xwalk:xwalk_core_library:15.44.384.12'
}

node {
    version = '4.2.3'
    npmVersion = '2.14.4'
    distBaseUrl = 'http://nodejs.org/dist'
    download = true
}

preBuild.dependsOn npmRunCopyCore
