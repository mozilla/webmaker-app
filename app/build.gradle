apply plugin: 'com.android.application'
apply plugin: 'com.moowork.node'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.mozilla.webmaker"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 7
        versionName "1.1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    flavorDimensions "abi"

    productFlavors {
        x86 {
            dimension "abi"
            ndk {
                abiFilters "x86", ""
            }
            versionCode = 3
        }
        armv7 {
            dimension "abi"
            ndk {
                abiFilters "armeabi-v7a", ""
            }
            versionCode = 1
        }
    }

    applicationVariants.all { variant ->
        // get the version code of each flavor
        def abiVersion = variant.productFlavors.get(0).versionCode

        // set the composite code
        variant.mergedFlavor.versionCode = abiVersion * 100000 + defaultConfig.versionCode
    }
}

task npmRunBuild (type: NpmTask, dependsOn: ['npmInstall']) {
    args = ['run', 'build']
}

repositories {
    mavenCentral()
    maven {
        url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v13:22.1.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.google.android.gms:play-services-analytics:7.5.0'
    compile 'org.xwalk:xwalk_core_library:13.42.319.12'
}

node {
    version = '0.12.2'
    npmVersion = '2.7.4'
    distBaseUrl = 'http://nodejs.org/dist'
    download = true
}

preBuild.dependsOn npmRunBuild